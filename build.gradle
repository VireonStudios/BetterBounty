plugins {
    id 'java'
    id 'io.github.goooler.shadow' version "8.1.7"
}

group = 'dev.vireon'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url "https://repo.triumphteam.dev/snapshots"
    }
    maven { url 'https://jitpack.io' }
    maven {
        name = "tcoded-releases"
        url = "https://repo.tcoded.com/releases"
    }
}

dependencies {
    compileOnly('org.projectlombok:lombok:1.18.30')
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation("dev.triumphteam:triumph-cmd-bukkit:2.0.0-SNAPSHOT") { exclude group: "com.google.code.gson" }
    implementation "dev.triumphteam:triumph-gui-paper:3.1.13-SNAPSHOT"

    implementation "de.rapha149.signgui:signgui:2.5.4"

    implementation "com.tcoded:FoliaLib:0.5.1"

    implementation "org.bstats:bstats-bukkit:3.0.2"

    compileOnly('com.github.MilkBowl:VaultAPI:1.7.1') {
        exclude group: 'org.bukkit'
    }

    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    dependsOn(jar)

    archiveFileName = "${project.name} v${archiveVersion.get()}.jar"

    relocate 'dev.triumphteam.gui', 'dev.vireon.bounty.libraries.gui'
    relocate 'dev.triumphteam.cmd', 'dev.vireon.bounty.libraries.cmd'
    relocate 'de.rapha149.signgui', 'dev.vireon.bounty.libraries.signgui'
    relocate 'com.tcoded.folialib', 'dev.vireon.bounty.libraries.folialib'
    relocate 'org.bstats', 'dev.vireon.bounty.libraries.bstats'

    from sourceSets.getByName("main").output
    configurations = [project.configurations.getByName("runtimeClasspath")]
}

build {
    dependsOn shadowJar
}